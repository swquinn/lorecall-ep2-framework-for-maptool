[h:tokenId = getStrProp(macro.args, "tokenId")]
[h:actionId = getStrProp(macro.args, "actionId")]
[h:color = "#1fa7d1"]

[h:firstRoll = 1d10]
[h:firstRoll=if(firstRoll==10, 0, firstRoll)]

[h:secondRoll = 1d10]
[h:secondRoll=if(secondRoll==10, 0, secondRoll)]

[h:compositeRoll=number(strformat("%{firstRoll}%{secondRoll}"))]
[h:compositeRoll=if(compositeRoll==0, 100, compositeRoll)]

[h:action=mob.getAction(actionId)]
[h:actionName=json.get(action, "name")]

[h:testModifiers=mob.getAttackModifiers()]
[h:testBonus=json.get(testModifiers, "totalBonus")]
[h:testPenalty=json.get(testModifiers, "totalPenalty")]

[h:testTotalModifier=testBonus + testPenalty]
[h:testTotalModifier=if(testTotalModifier<-60, -60, testTotalModifier)]
[h:testTotalModifier=if(testTotalModifier>60, 60, testTotalModifier)]

[h:baseTestDifficulty=mob.getSkillCheck(json.get(action, "skill"))]
[h:testDifficulty=baseTestDifficulty + testTotalModifier]
[h:testDifficulty=if(testDifficulty<0, 0, testDifficulty)]
[h:testDifficulty=if(testDifficulty>98, 98, testDifficulty)]
<table style="border:1px solid [r:string(color)]; width: 170px;">
  <tr>
    <td colspan="2" style="padding: 0px 2px; background-color:[r:string(color)]; color: #ffffff">
      <strong>[r: actionName] Action ([r:testDifficulty]%)</strong>
    </td>
  </tr>
  <tr style="vertical-align: middle;">
    [h:rollColor=color]
    [h:rollColor=if(compositeRoll>testDifficulty, "#ababab", rollColor)]
    [h:rollColor=if(compositeRoll==99, "#c93a48", rollColor)]
    [h:rollColor=if(compositeRoll==100, "#18a537", rollColor)]
    <td style="color: [r:string(rollColor)]; font-size: 24pt; text-align: center; vertical-align: middle;">
      <span><strong>[r:firstRoll]</strong></span>
      <span><strong>[r:secondRoll]</strong></span>
      [if(firstRoll==secondRoll), code: {
        <span>!</span>
      };{}]
    </td>
    <td style="text-align: center;">
      [h:superiorSuccesses=0]
      [h:superiorSuccesses=superiorSuccesses + if(compositeRoll>=33, 1, 0)]
      [h:superiorSuccesses=superiorSuccesses + if(compositeRoll>=66, 1, 0)]
      <div style="color: #484848; font-size: 10pt;">
        <span>[r:strformat("Successes: +%{superiorSuccesses}")]</span>
      </div>

      [h:superiorFailures=0]
      [h:superiorFailures=superiorFailures + if(compositeRoll<=66, 1, 0)]
      [h:superiorFailures=superiorFailures + if(compositeRoll<=33, 1, 0)]
      <div style="color: #484848; font-size: 10pt;">
        <span>[r:strformat("Failures: -%{superiorFailures}")]</span>
      </div>
    </td>
  </tr>
  <tr style="font-size: 7px">
    <td colspan="2">
      <h5 style="margin: 5px 0px 0px;">Target Before Modifiers ([r:baseTestDifficulty]%)</h5>
      <h5 style="margin: 5px 0px 0px;">Applied Bonuses ([r:testBonus]%)</h5>
      [h:bonusModifiers=json.get(testModifiers, "bonuses")]
      [foreach(item, bonusModifiers, "", ""), CODE: {
        [h:bonusModifier=json.get(bonusModifiers, item)]
        [h:text=json.get(bonusModifier, "text")]
        [h:mod=json.get(bonusModifier, "modifier")]
        [if(mod>0), CODE: {
          <p style="margin-left: 5px;">[r:strformat("+%{mod}% %{text}")]</p>
        };{}]
      }]

      <h5 style="margin: 5px 0px 0px;">Applied Penalties ([r:testPenalty]%)</h5>
      [h:penaltyModifiers=json.get(testModifiers, "penalties")]
      [foreach(item, penaltyModifiers, "", ""), CODE: {
        [h:penaltyModifier=json.get(penaltyModifiers, item)]
        [h:text=json.get(penaltyModifier, "text")]
        [h:mod=json.get(penaltyModifier, "modifier")]
        [if(mod<0), CODE: {
          <p style="margin-left: 5px;">[r:strformat("%{mod}% %{text}")]</p>
        };{}]
      }]
    </td>
  </tr>
  <tr>
    <td colspan="2" style="padding: 0px 2px; background-color:[r:string(color)]; color: #ffffff; text-align: center;">
      [h:rollDamageArgs=""]
      [h:rollDamageArgs=setStrProp(rollDamageArgs, "actionId", actionId)]
      [h:rollDamageArgs=setStrProp(rollDamageArgs, "tokenId", tokenId)]
      [h:rollDamageLink=macroLinkText("RollDamage@Lib:mob", "all", rollDamageArgs, tokenId)]
      <a style="color: #ffffff; text-decoration: none;" href="[r:rollDamageLink]">Roll Damage</a>
    </td>
  </tr>
</table>